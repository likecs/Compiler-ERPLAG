<program> = <moduleDeclarations> <otherModules> <driverModule> <otherModules>
<moduleDeclarations> = <moduleDeclaration> <moduleDeclarations>
<moduleDeclarations> = e
<moduleDeclaration> = DECLARE MODULE ID SEMICOL
<otherModules> = <module> <otherModules>
<otherModules> = e
<driverModule> = DRIVERDEF DRIVER PROGRAM DRIVERENDDEF <moduleDef>
<module> = DEF MODULE ID ENDDEF TAKES INPUT SQBO <input_plist> SQBC SEMICOL <ret> <moduleDef>
<ret> = RETURNS SQBO <output_plist> SQBC SEMICOL
<ret> = e
<input_plist> = ID COLON <dataType> <N1>
<N1> = COMMA ID COLON <dataType> <N1>
<N1> = e
<output_plist> = ID COLON <type> <N2>
<N2> = COMMA ID COLON <type> <N2>
<N2> = e
<dataType> = INTEGER
<dataType> = REAL
<dataType> = BOOLEAN
<dataType> = ARRAY SQBO <range> SQBC OF <type>
<type> = INTEGER
<type> = REAL
<type> = BOOLEAN
<moduleDef> = START <statements> END
<statements> = <statement> <statements>
<statements> = e
<statement> = <ioStmt>
<statement> = <simpleStmt>
<statement> = <declareStmt>
<statement> = <conditionalStmt>
<statement> = <iterativeStmt>
<ioStmt> = GET_VALUE BO ID BC SEMICOL
<ioStmt> = PRINT BO <var> BC SEMICOL
<var> = ID <whichID>
<var> = NUM
<var> = RNUM
<whichID> = SQBO ID SQBC
<whichID> = e
<simpleStmt> = <assignmentStmt>
<simpleStmt> = <moduleReuseStmt>
<assignmentStmt> = ID <whichStmt>
<whichStmt> = <lvalueIDStmt>
<whichStmt> = <lvalueARRStmt>
<lvalueIDStmt> = ASSIGNOP <expression> SEMICOL
<lvalueARRStmt> = SQBO <index_nt> SQBC ASSIGNOP <expression> SEMICOL
<index_nt> = NUM
<index_nt> = ID
<moduleReuseStmt> = <optional> USE MODULE ID WITH PARAMETERS <idList> SEMICOL
<optional> = SQBO <idList> SQBC ASSIGNOP
<optional> = e
<idList> = ID <N3>
<N3> = COMMA ID <N3>
<N3> = e
<expression> = <arithmeticOrBooleanExpr>
<expression> = MINUS BO <arithmeticExpr> BC
<arithmeticOrBooleanExpr> = <AnyTerm> <N7>
<N7> = <logicalOp> <AnyTerm> <N7>
<N7> = e
<AnyTerm> = <arithmeticExpr> <N8>
<N8> = <relationalOp> <arithmeticExpr> <N8>
<N8> = e
<arithmeticExpr> = <term_1> <N4>
<N4> = <op1> <term_1> <N4>
<N4> = e
<term_1> = <factor> <N5>
<N5> = <op2> <factor> <N5>
<N5> = e
<factor> = <var>
<op1> = PLUS
<op1> = MINUS
<op2> = MUL
<op2> = DIV
<logicalOp> = AND
<logicalOp> = OR
<relationalOp> = LT
<relationalOp> = LE
<relationalOp> = GT
<relationalOp> = GE
<relationalOp> = EQ
<relationalOp> = NE
<declareStmt> = DECLARE <idList> COLON <dataType> SEMICOL
<conditionalStmt> = SWITCH BO ID BC START <caseStmts> <default_1> END
<caseStmts> = CASE <value> COLON <statements> BREAK SEMICOL <N9>
<N9> = CASE <value> COLON <statements> BREAK SEMICOL <N9>
<N9> = e
<value> = NUM
<value> = TRUE_
<value> = FALSE_
<default_1> = DEFAULT COLON <statements> BREAK SEMICOL
<default_1> = e
<iterativeStmt> = FOR BO ID IN <range> BC START <statements> END
<iterativeStmt> = WHILE BO <expression> BC START <statements> END
<range> = NUM RANGEOP NUM
<factor> = BO <arithmeticOrBooleanExpr> BC
